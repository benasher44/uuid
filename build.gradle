buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.30'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        targetFromPreset(presets.jvm, 'jvm') {
            configure([compilations.main, compilations.test]) {
                tasks.getByName(compileKotlinTaskName).kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }
        targetFromPreset(presets.js, 'js') {
            configure([compilations.main, compilations.test]) {
                tasks.getByName(compileKotlinTaskName).kotlinOptions {
                    sourceMap = true
                    moduleKind = "umd"
                    metaInfo = true
                }
            }
        }
        targetFromPreset(presets.macosX64, 'macos')
        targetFromPreset(presets.iosX64, 'iosSim')
        targetFromPreset(presets.iosArm64, 'iosDevice64')
        targetFromPreset(presets.iosArm32, 'iosDevice32')
        targetFromPreset(presets.mingwX64, 'mingw') {
            compilations.test.linkerOpts "-Wl,--subsystem,windows, -Bstatic -lbcrypt"
        }
        targetFromPreset(presets.linuxX64, 'linux64')
        targetFromPreset(presets.linuxArm32Hfp, 'linux32')
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        def nix64MainSourceSets = [
                'src/nativeMain/kotlin',
                'src/nix64Main/kotlin'
        ]

        def nix32MainSourceSets = [
                'src/nativeMain/kotlin',
                'src/nix32Main/kotlin'
        ]

        macosMain { kotlin.srcDirs += nix64MainSourceSets }
        macosTest { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        iosDevice64Main { kotlin.srcDirs += nix64MainSourceSets }
        iosDevice64Test { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        iosDevice32Main { kotlin.srcDirs += nix32MainSourceSets }
        iosDevice32Test { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        iosSimMain { kotlin.srcDirs += nix64MainSourceSets }
        iosSimTest { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        mingwMain {
            kotlin.srcDirs += [
                    'src/nativeMain/kotlin',
                    'src/mingwMain/kotlin'
            ]
        }
        linux64Main { kotlin.srcDirs += nix64MainSourceSets }
        linux32Main { kotlin.srcDirs += nix32MainSourceSets }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.30.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

apply from: 'publish.gradle'
