buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21'
    }
}

repositories {
    jcenter()
    mavenCentral()
}
group 'com.benasher44'
version '0.0.1'

apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'

kotlin {
    targets {
        targetFromPreset(presets.jvm, 'jvm')
        targetFromPreset(presets.js, 'js')
        targetFromPreset(presets.macosX64, 'macos')
        targetFromPreset(presets.iosX64, 'iosSim')
        targetFromPreset(presets.iosArm64, 'iosDevice64')
        targetFromPreset(presets.iosArm32, 'iosDevice32')
        targetFromPreset(presets.mingwX64, 'mingw') {
            compilations.test.linkerOpts "-Wl,--subsystem,windows, -Bstatic -lbcrypt"
        }
        targetFromPreset(presets.linuxX64, 'linux64')
        targetFromPreset(presets.linuxArm32Hfp, 'linux32')
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        def cocoa64MainSourceSets = [
                'src/nativeMain/kotlin',
                'src/nix64Main/kotlin'
        ]

        def cocoa32MainSourceSets = [
                'src/nativeMain/kotlin',
                'src/nix32Main/kotlin'
        ]

        macosMain { kotlin.srcDirs += cocoa64MainSourceSets }
        macosTest { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        iosDevice64Main { kotlin.srcDirs += cocoa64MainSourceSets }
        iosDevice64Test { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        iosDevice32Main { kotlin.srcDirs += cocoa32MainSourceSets }
        iosDevice32Test { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        iosSimMain { kotlin.srcDirs += cocoa64MainSourceSets }
        iosSimTest { kotlin.srcDirs += 'src/cocoaTest/kotlin' }
        mingwMain {
            kotlin.srcDirs += [
                    'src/nativeMain/kotlin',
                    'src/mingwMain/kotlin'
            ]
        }
        linux64Main { kotlin.srcDirs += 'src/nix64Main/kotlin' }
        linux32Main { kotlin.srcDirs += 'src/nix32Main/kotlin' }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.30.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
